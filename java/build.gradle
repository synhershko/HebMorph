plugins {
    id "com.jfrog.bintray" version "1.8.4"
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'signing'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group = 'com.code972.hebmorph'
version = '7.6.0'
description = "Enabling Hebrew search in Lucene. Part of the HebMorph project, " +
        "for making Hebrew properly searchable"

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    jcenter()
}

dependencies {
    compile group: 'org.apache.lucene', name: 'lucene-core', version: version
    compile group: 'org.apache.lucene', name: 'lucene-analyzers-common', version:version
    compile group: 'org.apache.lucene', name: 'lucene-queryparser', version:version
    testCompile group: 'org.apache.lucene', name: 'lucene-memory', version:version
    testCompile group: 'org.apache.lucene', name: 'lucene-test-framework', version:version
    testCompile group: 'org.apache.commons', name: 'commons-lang3', version:'3.6'
    testCompile group: 'junit', name: 'junit', version:'4.12'
}

bintray {
    user = 'synhershko'
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']

    pkg {
        repo = "maven"
        name = "hebmorph-lucene"
        websiteUrl = "http://code972.com/hebmorph"
        vcsUrl = "https://github.com/synhershko/hebmorph.git"
        licenses = ['AGPL-V3']
        publish = true
        version {
            name = project.version
            released = new Date()
        }
    }
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "Affero GPL3"
            url "http://www.gnu.org/licenses/agpl.html"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "synhershko"
            name "Itamar Syn-Hershko"
            email "itamar@code972.com"
        }
    }
    
    scm {
       url "https://github.com/synhershko/HebMorph"
       connection "scm:git:ssh://git@github.com/synhershko/HebMorph.git"
       developerConnection "scm:git:ssh://git@github.com/synhershko/HebMorph.git"
    }
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId group
            artifactId 'hebmorph-lucene'
            version version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', description)
                root.appendNode('name', 'hebmorph-lucene')
                root.appendNode('url', 'http://code972.com/hebmorph')
                root.children().last() + pomConfig
            }
        }
    }
}
